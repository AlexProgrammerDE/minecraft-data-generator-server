plugins {
    id 'fabric-loom'
}

repositories {
    maven {
        name = "u9g"
        url = "https://maven.u9g.dev"
    }
}

loom {
    setIntermediaryUrl('https://maven.legacyfabric.net/net/fabricmc/intermediary/%1$s/intermediary-%1$s-v2.jar');
    customMinecraftManifest.set("https://legacyfabric.u9g.dev/v2/manifest/${minecraft_version}")

     // Required by 1.7.x
     runs {
     	client {
     		programArgs "--userProperties", "{}"
     	}
     }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API provides hooks for events, item registration, and more. As most mods will need this, it's included by default.
    // If you know for a fact you don't, it's not required and can be safely removed.
//    modImplementation ("net.legacyfabric.legacy-fabric-api:legacy-fabric-api:${fabric_version}") {
//        exclude module: "legacy-fabric-entity-events-v1"
//    }

    implementation(projects.common)

    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        implementation 'org.lwjgl.lwjgl:lwjgl_util:2.9.4-nightly-20150209'
        implementation 'org.lwjgl.lwjgl:lwjgl:2.9.4-nightly-20150209'
        implementation 'org.lwjgl.lwjgl:lwjgl-platform:2.9.4-nightly-20150209'
    }
}

if (System.getProperty("os.name").toLowerCase().contains("mac")) {
    configurations.configureEach {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('org.lwjgl.lwjgl:lwjgl_util:2.9.2-nightly-201408222') with module('org.lwjgl.lwjgl:lwjgl_util:2.9.4-nightly-20150209')
                substitute module('org.lwjgl.lwjgl:lwjgl:2.9.2-nightly-201408222') with module('org.lwjgl.lwjgl:lwjgl:2.9.4-nightly-20150209')
            }
            force 'org.lwjgl.lwjgl:lwjgl-platform:2.9.4-nightly-20150209'
        }
    }
}

processResources {
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}
